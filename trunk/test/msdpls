#!/usr/local/bin/perl -w

use strict;

use SNMP_Session;
use BER;
use Socket;

my $version = '1';
my $port = 161;
my $debug = 0;
my $css_stylesheet = 'http://www.switch.ch/lan/css/tf-ten.css';

### Prototypes
sub msdp_list_duplicate_sas ($ );
sub msdp_collect_sas ($ );
sub msdp_report_duplicate_sas ($$);
sub msdp_duplicate_report_header ($$$ );
sub msdp_duplicate_report_trailer ($ );
sub msdp_map_group ($$$ );
sub msdp_list_group ($$);
sub pretty_ip ($ );

while (defined $ARGV[0] && $ARGV[0] =~ /^-/) {
    if ($ARGV[0] =~ /^-v/) {
	if ($ARGV[0] eq '-v') {
	    shift @ARGV;
	    usage (1) unless defined $ARGV[0];
	} else {
	    $ARGV[0] = substr($ARGV[0], 2);
	}
	if ($ARGV[0] eq '1') {
	    $version = '1';
	} elsif ($ARGV[0] eq '2c') {
	    $version = '2c';
	} else {
	    usage (1);
	}
    } elsif ($ARGV[0] =~ /^-p/) {
	if ($ARGV[0] eq '-p') {
	    shift @ARGV;
	    usage (1) unless defined $ARGV[0];
	} else {
	    $ARGV[0] = substr($ARGV[0], 2);
	}
	if ($ARGV[0] =~ /^[0-9]+$/) {
	    $port = $ARGV[0];
	} else {
	    usage (1);
	}
    } elsif ($ARGV[0] eq '-h') {
	usage (0);
	exit 0;
    } else {
	usage (1);
    }
    shift @ARGV;
}
my $host = shift @ARGV || usage (1);
my $community = shift @ARGV || "public";
usage (1) if $#ARGV >= $[;
my $session =
    ($version eq '1' ? SNMPv1_Session->open ($host, $community, $port)
     : $version eq '2c' ? SNMPv2c_Session->open ($host, $community, $port)
     : die "Unknown SNMP version $version")
  || die "Opening SNMP_Session";
$session->debug (1) if $debug;
$session->{max_repetitions} = 100;

my $msdpSACachePeerLearnedFrom = [1,3,6,1,3,92,1,1,6,1,4];
my $msdpSACacheRPFPeer = [1,3,6,1,3,92,1,1,6,1,5];
my $msdpSACacheInSAs = [1,3,6,1,3,92,1,1,6,1,6];
my $msdpSACacheInDataPackets = [1,3,6,1,3,92,1,1,6,1,7];
my $msdpSACacheUpTime = [1,3,6,1,3,92,1,1,6,1,8];
my $msdpSACacheExpiryTime = [1,3,6,1,3,92,1,1,6,1,9];
my $msdpSACacheStatus = [1,3,6,1,3,92,1,1,6,1,10];

msdp_list_duplicate_sas ($session);

# msdp_list_group ($session, inet_aton ("233.2.171.1"));
1;

sub msdp_list_duplicate_sas ($ ) {
    my ($session) = @_;
    my ($announcements, $nsas) = msdp_collect_sas ($session);
    msdp_report_duplicate_sas ($announcements, $nsas);
}

sub msdp_collect_sas ($ ) {
    my ($session) = @_;
    my @oids = ($msdpSACacheStatus);
    my $nsa = 0;
    my %announcements;
    $session->map_table
	(\@oids,
	 sub () {
	     my ($index, $sa_status) = @_;
	     die "index: $index"
		 unless $index =~ /^(\d+\.\d+\.\d+\.\d+)\.(\d+\.\d+\.\d+\.\d+)\.(\d+\.\d+\.\d+\.\d+)$/;
	     my ($group, $source, $rp) = ($1, $2, $3);
	     return 0 unless pretty_print ($sa_status) == 1; # active(1)
	     ++$nsa;
	     push @{$announcements{$source,$group}}, {rp => $rp};
	 });
    (\%announcements, $nsa);
}

sub msdp_report_duplicate_sas ($$) {
    my ($announcements, $nsas) = @_;
    my $dupannouncements = 0;
    foreach my $key (keys %{$announcements}) {
	my $anns = $announcements->{$key};
	if ($#{$anns} > 0) {
	    ++$dupannouncements;
	} else {
	    undef $announcements->{$key};
	}
    }
    msdp_duplicate_report_header ($announcements, $nsas, $dupannouncements);
    foreach my $key (sort keys %{$announcements}) {
	my ($source, $group) = split ($;,$key);
	my $announcements = $announcements->{$key};
	if ($#{$announcements} > 0) {
	    printf STDOUT "<h4> (%s,%s) </h4>\n<pre>\n", pretty_ip ($source), pretty_ip ($group);
	    foreach my $announcement (@{$announcements}) {
		printf STDOUT "  RP %s\n", pretty_ip ($announcement->{rp});
	    }
	    printf STDOUT "</pre>\n";
	}
    }
    msdp_duplicate_report_trailer ($announcements);
}

sub msdp_duplicate_report_header ($$$ ) {
    my ($announcements, $nsas, $dupannouncements) = @_;
    my $nsgs = keys %{$announcements};
    print STDOUT "<html><head><title>MSDP Duplicate SA Report</title></head>\n";
    printf STDOUT "<LINK REL=STYLESHEET HREF=\"%s\" TYPE=\"text/css\">\n",
    $css_stylesheet
	if defined $css_stylesheet;
    print STDOUT "<body><h1>MSDP Duplicate SA Report</h1>\n";
    printf STDOUT "<p> %d (S,G) pairs found in %d SAs in %s's cache. \n", $nsgs, $nsas, $host;
    printf STDOUT "Total number of (S,G) pairs with multiple RPs: %d </p>\n", $dupannouncements;
}

sub msdp_duplicate_report_trailer ($ ) {
    my ($announcements) = @_;
    print STDOUT "</body></html>\n";
}

sub msdp_map_group ($$$) {
    my ($session, $group, $mapfn) = @_;
    my @group_subids = split (/\./, inet_ntoa ($group), 4);
    my @oids = ([@{$msdpSACachePeerLearnedFrom},@group_subids],
		[@{$msdpSACacheRPFPeer},@group_subids],
		[@{$msdpSACacheInSAs},@group_subids],
		[@{$msdpSACacheInDataPackets},@group_subids],
		[@{$msdpSACacheUpTime},@group_subids],
		[@{$msdpSACacheExpiryTime},@group_subids],
		[@{$msdpSACacheStatus},@group_subids]);
    $session->map_table
	(\@oids,
	 sub () {
	     my ($index,
		 $peer_learned_from,$rpf_peer,
		 $in_sas,$in_data_packets,
		 $up_time,$expiry_time,
		 $status) = @_;
	     map { $_ = pretty_print $_ if defined $_ }
	     ($peer_learned_from,$rpf_peer,$in_sas,$in_data_packets,
	      $up_time,$expiry_time,$status);
	     my ($source,$rp);
	     (($source,$rp) = ($index =~ /^([0-9]+\.[0-9]+\.[0-9]+\.[0-9]+)\.([0-9]+\.[0-9]+\.[0-9]+\.[0-9]+)$/))
		 || die "?";
	     &$mapfn ($source,$rp,$peer_learned_from,$rpf_peer,
		      $in_sas,$in_data_packets,
		      $up_time,$expiry_time,
		      $status);
});
}

sub msdp_list_group ($$) {
    my ($session, $group) = @_;
    msdp_map_group ($session,$group,
		    sub () {
			my ($source,$rp,
			    $peer_learned_from,$rpf_peer,
			    $in_sas,$in_data_packets,
			    $up_time,$expiry_time,$status) = @_;
			#return unless $in_data_packets;
			print "  ",pretty_ip ($source)," $in_data_packets pkts\n";
			print " $peer_learned_from (learned-from) != $rpf_peer (RPF peer)\n"
			    if $peer_learned_from ne $rpf_peer;
		    });
}

sub pretty_ip ($ ) {
    my ($ip) = @_;
    my ($name);

    ($name = gethostbyaddr (inet_aton ($ip),AF_INET))
	? "$name [$ip]"
	: "$ip";
}
